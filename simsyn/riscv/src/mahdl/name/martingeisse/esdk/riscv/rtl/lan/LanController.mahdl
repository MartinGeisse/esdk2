module name.martingeisse.esdk.riscv.rtl.lan.LanController;

/*
    Address map:
        0x000000                PHY management I/O register
        0x000004                received-packet consumer control
        0x000008                received-packet length (bus read)
        0x00000c                sender ready (bus read)
        0x00000c                to-send-packet length, start sending (bus write)
        0x001000 - 0x0017ff     receive buffer (bus read)
        0x001000 - 0x0017ff     send buffer (bus write)
 */

interface {

    in clock clk;
    in bit reset;

    in bit busEnable, busWrite;
    in vector[22] busWordAddress;
    in vector[32] busWriteData;
    out bit busAcknowledge;
    out vector[32] busReadData;

    out bit mdc, mdioOutWeak;
    in bit mdioIn;

    in bit rxDv, rxClk, rxEr;
    in vector[4] rxd;

    in bit txClk;
    out bit txEn, txEr;
    out vector[4] txd;

}

// bus output signals
register bit busReadSecondCycle;
do (*) {
    if (busWordAddress[10]) {
        busAcknowledge = busReadSecondCycle;
        busReadData = receiveBuffer.readData;
    } else {
        busAcknowledge = 1;
        busReadData = switch (busWordAddress[1:0]) {
            case 2d0: 31d0 _ mdioIn
            case 2d1: 31d0 _ (receiveState == RECEIVE_STATE_CONSUME_PACKET)
            case 2d2: 21d0 _ receivedPacketLengthRegister
            case 2d3: 31d0 _ (sendState == SEND_STATE_IDLE)
        };
    }
}
do (clk) {
    if (busEnable & busWordAddress[10]) {
        busReadSecondCycle = ~busReadSecondCycle;
    } else {
        busReadSecondCycle = 0;
    }
}

// PHY management interface
register bit mdcRegister = 0, mdioOutWeakRegister = 1;
do (*) {
    mdc = mdcRegister;
    mdioOutWeak = mdioOutWeakRegister;
}
do (clk) {
    if (reset) {
        mdcRegister = 0;
        mdioOutWeakRegister = 1;
    } else if (busEnable & busWrite) {
        if (~busWordAddress[10] & busWordAddress[1:0] == 2d0) {
            mdioOutWeakRegister = busWriteData[0];
            mdcRegister = busWriteData[1];
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
// receiving
// --------------------------------------------------------------------------------------------------------------------

// rx_clk edge detector
register bit previousRxClk;
signal bit rxClkRisingEdge;
do (*) {
    rxClkRisingEdge = rxClk & ~previousRxClk;
}
do (clk) {
    previousRxClk = rxClk;
}

// receiver state machine
register vector[3] receiveState;
constant vector[3] RECEIVE_STATE_SKIP_PACKET = 3d0;
constant vector[3] RECEIVE_STATE_IDLE = 3d1;
constant vector[3] RECEIVE_STATE_RECEIVING = 3d2;
constant vector[3] RECEIVE_STATE_APPEND_PADDING = 3d3;
constant vector[3] RECEIVE_STATE_CONSUME_PACKET = 3d4;
do (clk) {
    if (reset) {
        receiveState = RECEIVE_STATE_SKIP_PACKET;
    } else switch (receiveState) {

        case RECEIVE_STATE_SKIP_PACKET:
            receiveBufferPointer = 12d0;
            if (~rxDv) {
                receiveState = RECEIVE_STATE_IDLE;
            }

        case RECEIVE_STATE_IDLE:
            if (rxClkRisingEdge & rxDv) {
                receiveBufferPointer = receiveBufferPointer + 12d1;
                if (rxEr) {
                    receiveState = RECEIVE_STATE_SKIP_PACKET;
                } else {
                    receiveState = RECEIVE_STATE_RECEIVING;
                }
            }

        case RECEIVE_STATE_RECEIVING:
            if (rxClkRisingEdge) {
                receiveBufferPointer = receiveBufferPointer + 12d1;
                if (~rxDv) {
                    receivedPacketLengthRegister = receiveBufferPointer[11:1]; // nibbles to bytes
                    receiveState = RECEIVE_STATE_APPEND_PADDING;
                } else if (rxEr) {
                    receiveState = RECEIVE_STATE_SKIP_PACKET;
                } else {
                    receiveState = RECEIVE_STATE_RECEIVING;
                }
            }

        case RECEIVE_STATE_APPEND_PADDING:
            receiveBufferPointer = receiveBufferPointer + 12d1;
            if (receiveBufferPointer[2:0] == 3d0) {
                receiveState = RECEIVE_STATE_CONSUME_PACKET;
            }

        case RECEIVE_STATE_CONSUME_PACKET:
            if (busEnable & busWrite & ~busWordAddress[10] & busWordAddress[1:0] == 2d1) {
                receiveState = RECEIVE_STATE_SKIP_PACKET; // might happen in the middle of a second packet, so skip
            }

    }
}

// receive buffer
name.martingeisse.esdk.riscv.rtl.lan.ReceiveBuffer receiveBuffer;
register vector[12] receiveBufferPointer;
register vector[11] receivedPacketLengthRegister;
do (*) {
    receiveBuffer.clk = clk;
    receiveBuffer.writeEnable = rxClkRisingEdge & rxDv & (receiveState == RECEIVE_STATE_IDLE |
            receiveState == RECEIVE_STATE_RECEIVING | receiveState == RECEIVE_STATE_APPEND_PADDING);
    receiveBuffer.writeAddress = receiveBufferPointer;
    receiveBuffer.writeData = rxDv ? rxd : 4d0;
    receiveBuffer.readAddress = busWordAddress[8:0];
}

// --------------------------------------------------------------------------------------------------------------------
// sending
// --------------------------------------------------------------------------------------------------------------------

// tx_clk edge detector
register bit previousTxClk;
signal bit txClkRisingEdge;
do (*) {
    txClkRisingEdge = txClk & ~previousTxClk;
}
do (clk) {
    previousTxClk = txClk;
}

// Generate txd, txEn, txEr. Note that we don't output the value of the BlockRAM output register directly to the txd
// pins since our time budget for the txd setup time would become rather small then, since the BlockRAM address gets
// changed in reaction to the txClk edge. Draw a timing diagram to understand this, or just accept that txd gets
// fed through an extra register before it reaches the pin.
register bit txEnRegister = 0;
register vector[4] txdRegister;
do (*) {
    txEn = txEnRegister;
    txd = txdRegister;
    txEr = 0; // we don't send artificial error symbols
}

// send state machine
register vector[2] sendState;
constant vector[2] SEND_STATE_IDLE = 2d0; // note: software must make sure we obey interpacket gap
constant vector[2] SEND_STATE_PREPARE = 2d1;
constant vector[2] SEND_STATE_SENDING = 2d2;
do (clk) {
    if (busEnable & busWrite & ~busWordAddress[10] & busWordAddress[1:0] == 2d3) {
        sendBufferLengthRegister = busWriteData[10:0];
    }
    if (reset) {
        sendState = SEND_STATE_IDLE;
    } else switch (sendState) {

        case SEND_STATE_IDLE:
            sendBufferPointer = 12d0;
            if (busEnable & busWrite & ~busWordAddress[10] & busWordAddress[1:0] == 2d3) {
                sendState = SEND_STATE_PREPARE;
            }

        // we need this one extra cycle to make sure the first nibble has been read from the send buffer (it's a
        // synchronous BlockRAM!) *before* SEND_STATE_SENDING detects a rising tx_clk edge.
        case SEND_STATE_PREPARE:
            sendState = SEND_STATE_SENDING;

        // In SEND_STATE_SENDING, we don't actually start sending (i.e. txEn stays low) until we have seen the first
        // tx_clk edge. This makes sure that txEn and txd obey their setup time.
        case SEND_STATE_SENDING:
            if (txClkRisingEdge) {
                if (sendBufferPointer[11:1] == sendBufferLengthRegister) {
                    txEnRegister = 0;
                    sendState = SEND_STATE_IDLE;
                } else {
                    txEnRegister = 1;
                }
                txdRegister = sendBuffer.readData;
                sendBufferPointer = sendBufferPointer + 12d1;
            }

    }
}

// send buffer
name.martingeisse.esdk.riscv.rtl.lan.SendBuffer sendBuffer;
register vector[12] sendBufferPointer;
register vector[11] sendBufferLengthRegister;
do (*) {
    sendBuffer.clk = clk;
    sendBuffer.writeEnable = busEnable & busWrite & busWordAddress[10];
    sendBuffer.writeData = busWriteData;
    sendBuffer.writeAddress = busWordAddress[8:0];
    sendBuffer.readAddress = sendBufferPointer;
}
