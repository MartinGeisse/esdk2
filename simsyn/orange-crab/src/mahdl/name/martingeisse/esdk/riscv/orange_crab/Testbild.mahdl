module name.martingeisse.esdk.riscv.orange_crab.Testbild;

interface {
    in clock clk;
    out bit ledRn, ledGn, ledBn;
    out vector[3] r, g, b;
    out bit hsync, vsync;
}

// heartbeat LED
register vector[24] counter = 0;
do (clk) {
    counter = counter + 24d1;
}
do (*) {
    ledRn = counter[23];
    ledGn = 1;
    ledBn = 1;
}

//
// stage 1: VGA timing
//

register vector[10] stage1x, stage1y;
register vector[1] stage1p;
register bit stage1hblank, stage1vblank, stage1hsync, stage1vsync;
do (clk) {
    stage1p = stage1p + 1d1;
    if (stage1p == 1d0) {
        if (stage1x == 10d799) {
            stage1hblank = 0;
            stage1x = 0;
            if (stage1y == 10d524) {
                stage1vblank = 0;
                stage1y = 0;
            } else {
                if (stage1y == 10d479) {
                    stage1vblank = 1;
                } else if (stage1y == 10d489) {
                    stage1vsync = 0;
                } else if (stage1y == 10d491) {
                    stage1vsync = 1;
                }
                stage1y = stage1y + 10d1;
            }
        } else {
            if (stage1x == 10d639) {
                stage1hblank = 1;
            } else if (stage1x == 10d655) {
                stage1hsync = 0;
            } else if (stage1x == 10d751) {
                stage1hsync = 1;
            }
            stage1x = stage1x + 10d1;
        }
    }
}


//
// stage 3 (non-registered): generate VGA signals
//

do (*) {
    r = (~stage1hblank & ~stage1vblank) ? stage1x[5:3] : 3b0;
    g = (~stage1hblank & ~stage1vblank) ? stage1y[5:3] : 3b0;
    b = (~stage1hblank & ~stage1vblank) ? (stage1x + stage1y)[8:6] : 3b0;
    hsync = stage1hsync;
    vsync = stage1vsync;
}
